// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    jcenter()
}

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    signingConfigs {
        grommet {
            storeFile file('../keys/grommet.keystore')
            storePassword System.env['KEYSTORE_PASSWORD']
            keyAlias 'grommetKey'
            keyPassword System.env['KEY_PASSWORD']
        }
        debug {
            storeFile file('../keys/debug.keystore')
            storePassword 'android'
            keyAlias 'grommetDebugKey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 24

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"

        signingConfig signingConfigs.grommet
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
        release{
            minifyEnabled false
        }
    }

    productFlavors {
        internal {
            applicationId 'com.rockthevote.grommet.internal'
        }
        production {
            applicationId 'com.rockethevote.grommet'
        }
    }

    lintOptions {
        abortOnError false
        ignore 'InvalidPackage'// Provided AutoValue pulls in Guava and friends. Doesn't end up in APK.
    }

    dexOptions {
        javaMaxHeapSize "4g"
        dexInProcess = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    sourceSets {
        internalDebug {
            assets.srcDirs = ['src/internalDebug/assets', 'src/internalDebug/assets/']
            res.srcDirs = ['src/internalDebug/res', 'src/internalDebug/res/']


            manifest.srcFile
            'src/internalDebug/AndroidManifest.xml'


            manifest.srcFile
            'src/internalDebug/AndroidManifest.xml'
        }
        internalRelease { res.srcDirs = ['src/internalRelease/res', 'src/internalRelease/res/'] }
        productionDebug { res.srcDirs = ['src/productionDebug/res', 'src/productionDebug/res/'] }
        productionRelease {
            res.srcDirs = ['src/productionRelease/res', 'src/productionRelease/res/']
        }
        internal { res.srcDirs = ['src/internal/res', 'src/internal/res/'] }
    }
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:${supportLibVersion}"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:support-v13:${supportLibVersion}"
    compile "com.android.support:support-annotations:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile 'com.google.android.gms:play-services-location:9.4.0'

    apt 'com.squareup.dagger:dagger-compiler:1.2.2'

    internalDebugCompile 'com.squareup.retrofit2:retrofit-mock:2.0.0'

    apt 'com.jakewharton:butterknife-compiler:8.0.1'

    provided 'com.google.auto.value:auto-value:1.2-rc1'
    apt 'com.google.auto.value:auto-value:1.2-rc1'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.3-rc2'
    apt 'com.ryanharter.auto.value:auto-value-moshi:0.4.0-rc2'
    provided 'com.ryanharter.auto.value:auto-value-moshi:0.4.0-rc2'

    internalDebugCompile 'com.jakewharton.madge:madge:1.1.3'
    internalDebugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
    internalDebugCompile 'com.jakewharton:process-phoenix:1.0.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-SNAPSHOT'

    internalCompile 'com.mattprecious.telescope:telescope:2.1.0'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    // TODO https://code.google.com/p/android-test-kit/issues/detail?id=157
    //androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.2.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.0.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'com.github.gcacace:signature-pad:1.2.0'
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'com.jakewharton.timber:timber:4.0.1'
    compile 'com.jakewharton.byteunits:byteunits:0.9.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'pl.charmas.android:android-reactive-location:0.9@aar'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.squareup.phrase:phrase:1.1.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.6.3'
    compile 'io.reactivex:rxjava:1.1.8'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.0'
    compile 'com.mobsandgeeks:android-saripaar:2.0.3'
    compile 'com.jaredrummler:material-spinner:1.0.9'
}

