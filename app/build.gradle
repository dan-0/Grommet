// Manifest version information!
def versionMajor = 2
def versionMinor = 8
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    jcenter()
}

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 26

    signingConfigs {
        grommet {
            storeFile file('../keys/grommet.keystore')
            storePassword System.env['KEYSTORE_PASSWORD']
            keyAlias 'grommetKey'
            keyPassword System.env['KEY_PASSWORD']
        }

        debug {
            storeFile file('../keys/debug.keystore')
            storePassword 'android'
            keyAlias 'grommetDebugKey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
//        multiDexEnabled true

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"

        signingConfig signingConfigs.grommet

        manifestPlaceholders = [HOCKEYAPP_APP_ID: "9aa043263d8e4edb902b860bf5098acb"]
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    flavorDimensions "default"

    productFlavors {
        internal {
            dimension "default"
            applicationId 'com.rockthevote.grommet.internal'
        }
        production {
            dimension "default"
            applicationId 'com.rockethevote.grommet'
        }
        practice {
            dimension "default"
            applicationId 'com.rockthevote.grommet.practice'
        }
    }

    lintOptions {
        abortOnError false
        ignore 'InvalidPackage'
// Provided AutoValue pulls in Guava and friends. Doesn't end up in APK.
    }

    dexOptions {
        javaMaxHeapSize "4g"
        dexInProcess = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    sourceSets {

        internalDebug {
            assets.srcDirs = ['src/internalDebug/assets', 'src/internalDebug/assets/']
            res.srcDirs = ['src/internalDebug/res', 'src/internalDebug/res/']


            manifest.srcFile
            'src/internalDebug/AndroidManifest.xml'


            manifest.srcFile
            'src/internalDebug/AndroidManifest.xml'
        }

        internalRelease {
            res.srcDirs = ['src/internalRelease/res', 'src/internalRelease/res/']
            manifest.srcFile 'src/internalRelease/AndroidManifest.xml'
        }

        productionDebug {
            res.srcDirs = ['src/productionDebug/res', 'src/productionDebug/res/']
        }

        productionRelease {
            res.srcDirs = ['src/productionRelease/res', 'src/productionRelease/res/']
        }

        internal {
            res.srcDirs = ['src/internal/res', 'src/internal/res/']
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:${supportLibVersion}"
}

// TODO remove eventually: http://b.android.com/162285
configurations {
    internalDebugImplementation
    internalReleaseImplementation
    productionReleaseImplementation
    practiceReleaseImplementation
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:support-v13:${supportLibVersion}"
    implementation "com.android.support:support-annotations:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation 'com.google.android.gms:play-services-location:11.8.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:7.5.2'

    annotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.2'

    internalDebugImplementation 'com.squareup.retrofit2:retrofit-mock:2.2.0'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.0.1'

    compileOnly 'com.google.auto.value:auto-value:1.5.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.5.2'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-moshi:0.4.0-rc2'
    compileOnly 'com.ryanharter.auto.value:auto-value-moshi:0.4.0-rc2'

    internalDebugImplementation 'com.jakewharton:process-phoenix:1.0.2'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.4'
    internalReleaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    productionReleaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    practiceReleaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'

    internalImplementation 'com.mattprecious.telescope:telescope:2.1.0'

    internalReleaseImplementation 'net.hockeyapp.android:HockeySDK:4.1.5'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
    // TODO https://code.google.com/p/android-test-kit/issues/detail?id=157
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation 'com.squareup.spoon:spoon-client:1.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.27'
    implementation 'com.squareup.dagger:dagger:1.2.2'
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.github.gcacace:signature-pad:1.2.0'
    implementation 'com.jakewharton:butterknife:8.0.1'
    implementation 'com.jakewharton.timber:timber:4.0.1'
    implementation 'com.jakewharton.byteunits:byteunits:0.9.1'
    implementation 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    implementation 'pl.charmas.android:android-reactive-location:0.9@aar'
    implementation 'com.squareup.sqlbrite:sqlbrite:0.6.3'
    implementation 'io.reactivex:rxjava:1.1.8'
    implementation 'io.reactivex:rxandroid:1.0.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.3'
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3'

    implementation 'com.mobsandgeeks:android-saripaar:2.0.3'
}

